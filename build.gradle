//file:noinspection GrUnresolvedAccess
//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.9.4'

    id 'jacoco'
}

group 'com.clementjean.grpc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    protoVersion = '3.23.4'
    grpcVersion = '1.55.3'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protoVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets.main.java.srcDir new File(buildDir, 'generated/source')

dependencies {
    runtimeOnly "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation "io.grpc:grpc-services:$grpcVersion"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "com.google.protobuf:protobuf-java-util:$protoVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.mockito:mockito-core:4.8.0'

    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    implementation 'io.opentelemetry:opentelemetry-api:1.28.0'
    implementation 'io.opentelemetry:opentelemetry-sdk:1.28.0'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.28.0'
    implementation 'io.opentelemetry:opentelemetry-exporter-jaeger:1.28.0'
    implementation 'io.opentelemetry:opentelemetry-semconv:1.28.0-alpha'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = false
        html.required = false
        csv.required = true
        csv.destination file("${buildDir}/reports/jacoco/jacoco.csv")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(
                dir: it,
                includes: [
                    "greeting/server/GreetingServiceImpl.class",
                    "calculator/server/CalculatorServiceImpl.class",
                    "blog/server/BlogServiceImpl.class",
                    "blog/client/BlogClient.class",
                ]
            )
        }))
    }
}